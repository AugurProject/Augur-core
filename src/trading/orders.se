# Copyright (C) 2015 Forecast Foundation OU, full GPL notice in LICENSE

#### must be first data members to match Delegator.sol
data controller
data controllerLookupName
####

macro ORDERS_FETCHER: self.controller.lookup('NewOrdersFetcher')

inset('../macros/externs.sem')
inset('../macros/require.sem')
inset('../macros/safeMath.sem')
inset('../macros/assertNoValue.sem')
inset('../macros/assertPrivateCall.sem')
inset('../macros/orderFields.sem')

### Storage of all data associated with orders

data owner
data initialized

# indices: marketID, outcomeID, type, orderID
data orders[](fxpAmount, fxpPrice, owner, fxpSharesEscrowed, fxpMoneyEscrowed, betterOrderID, worseOrderID, gasPrice)
# prices keeps track of the latest traded fxpPrice for a given outcome in a market [market is the key]
data marketOrderData[](volume, prices[])
# indices are marketID, outcomeID, and type
data bestOrder[]
data worstOrder[]

event CancelOrder(market: address: indexed, sender: address: indexed, fxpPrice: int256, fxpAmount: uint256, orderID: address, outcome: uint8, type: uint8, cashRefund: int256, sharesRefund: int256)
event CompleteSets(sender: address: indexed, market: address: indexed, type: indexed, fxpAmount: uint256, numOutcomes: int256, marketCreatorFee: int256, reportingFee: int256)
event MakeOrder(market: address: indexed, sender: address: indexed, type, fxpPrice: int256, fxpAmount: uint256, outcome: uint8, orderID: address, fxpMoneyEscrowed: int256, fxpSharesEscrowed: uint256, tradeGroupID: uint256)
event TakeOrder(market: address: indexed, outcome: uint8: indexed, type: uint8: indexed, orderID: address, price: int256, maker: address, taker:address, makerShares: int256, makerTokens: int256, takerShares: int256, takerTokens: int256, tradeGroupID: uint256)

# Trade types
macro BID: 1
macro ASK: 2

def any():
    assertNoValue()

def init():
    self.owner = msg.sender

def initialize(controller: address):
    require(not self.initialized)
    self.initialized = 1
    self.controller = controller
    return 1

# @public
# @return fxp
def getAmount(orderID: address, type: uint8, market: address, outcome: uint8):
    return(self.orders[orderID].fxpAmount)

# @public
# @return fxp
def getPrice(orderID: address, type: uint8, market: address, outcome: uint8):
    return(self.orders[orderID].fxpPrice)

# @public
def getOrderOwner(orderID: address, type: uint8, market: address, outcome: uint8):
    return(self.orders[orderID].owner)

# @public
# @return fxp
def getOrderSharesEscrowed(orderID: address, type: uint8, market: address, outcome: uint8):
    return(self.orders[orderID].fxpSharesEscrowed)

# @public
# @return fxp
def getOrderMoneyEscrowed(orderID: address, type: uint8, market: address, outcome: uint8):
    return(self.orders[orderID].fxpMoneyEscrowed)

# @public
# @return fxp
def getVolume(market: address):
    return(self.marketOrderData[market].volume)

# @public
# @return fxp
def getLastOutcomePrice(market: address, outcome: uint8):
    return(self.marketOrderData[market].prices[outcome])

# @public
def getBetterOrderId(orderID: address, type: uint8, market: address, outcome: uint8):
    return(self.orders[orderID].betterOrderID: address)

# @public
def getWorseOrderId(orderID: address, type: uint8, market: address, outcome: uint8):
    return(self.orders[orderID].worseOrderID: address)

# @public
def getGasPrice(orderID: address, type: uint8, market: address, outcome: uint8):
    return(self.orders[orderID].gasPrice)

# @public
def getBestOrderId(type: uint8, market: address, outcome: uint8):
    return(self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, type)])

# @public
def getWorstOrderId(type: uint8, market: address, outcome: uint8):
    return(self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, type)])

# @public
def getOrderIdHash(type: uint8, market: address, fxpAmount: uint256, fxpPrice: int256, sender: address, blockTimestamp: uint256, outcome: uint8, fxpMoneyEscrowed: int256, fxpSharesEscrowed: uint256):
    order = [type, market, fxpAmount, fxpPrice, sender, blockTimestamp, outcome, fxpMoneyEscrowed, fxpSharesEscrowed]
    return ripemd160(order, items=9)

# @public
def getOrderIdHash2():
    return 0

# @public
def isBetterPrice(type: uint8, market: address, outcome: uint8, fxpPrice, orderID: address):
    if(type == BID):
        return((fxpPrice > self.orders[orderID].fxpPrice))
    else:
        return((fxpPrice < self.orders[orderID].fxpPrice))

# @public
def isWorsePrice(type: uint8, market: address, outcome: uint8, fxpPrice, orderID: address):
    if(type == BID):
        return((fxpPrice < self.orders[orderID].fxpPrice))
    else:
        return((fxpPrice > self.orders[orderID].fxpPrice))

# @public
def assertIsNotBetterPrice(type: uint8, market: address, outcome: uint8, fxpPrice, betterOrderID: address):
    require(not self.isBetterPrice(type, market, outcome, fxpPrice, betterOrderID))
    return(1)

# @public
def assertIsNotWorsePrice(type: uint8, market: address, outcome: uint8, fxpPrice, worseOrderID: address):
    require(not self.isWorsePrice(type, market, outcome, fxpPrice, worseOrderID))
    return(1)

# @internal
def insertOrderIntoList(orderID: address, type: uint8, market: address, outcome: uint8, fxpPrice: int256, betterOrderID: address, worseOrderID: address):
    self.controller.assertIsWhitelisted(msg.sender)
    bestOrderID = self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, type)]
    worstOrderID = self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, type)]
    boundingOrders = ORDERS_FETCHER.findBoundingOrders(type, market, outcome, fxpPrice, bestOrderID, worstOrderID, betterOrderID, worseOrderID, outitems=2)
    betterOrderID = boundingOrders[0]
    worseOrderID = boundingOrders[1]
    if(type == BID):
        bestOrderID = self.updateBestBidOrder(orderID, market, fxpPrice, outcome, betterOrderID)
        worstOrderID = self.updateWorstBidOrder(orderID, market, fxpPrice, outcome, worseOrderID)
    else:
        bestOrderID = self.updateBestAskOrder(orderID, market, fxpPrice, outcome, betterOrderID)
        worstOrderID = self.updateWorstAskOrder(orderID, market, fxpPrice, outcome, worseOrderID)
    if(bestOrderID == orderID):
        betterOrderID = 0
    if(worstOrderID == orderID):
        worseOrderID = 0
    if(betterOrderID != 0):
        self.orders[betterOrderID].worseOrderID = orderID
        self.orders[orderID].betterOrderID = betterOrderID
    if(worseOrderID != 0):
        self.orders[worseOrderID].betterOrderID = orderID
        self.orders[orderID].worseOrderID = worseOrderID
    return(1)

# @internal
def saveOrder(orderID: address, type: uint8, market: address, fxpAmount: uint256, fxpPrice: int256, sender: address, outcome: uint8, fxpMoneyEscrowed: int256, fxpSharesEscrowed: uint256, betterOrderID: address, worseOrderID: address, tradeGroupID: uint256, gasPrice: uint256):
    self.controller.assertIsWhitelisted(msg.sender)
    require(type == BID or type == ASK)
    require(0 <= outcome and outcome < market.getNumberOfOutcomes())
    orderID = self.getOrderIdHash(type, market, fxpAmount, fxpPrice, sender, block.timestamp, outcome, fxpMoneyEscrowed, fxpSharesEscrowed)
    self.insertOrderIntoList(orderID, type, market, outcome, fxpPrice, betterOrderID, worseOrderID)
    self.orders[orderID].fxpPrice = fxpPrice
    self.orders[orderID].fxpAmount = fxpAmount
    self.orders[orderID].owner = sender
    self.orders[orderID].fxpMoneyEscrowed = fxpMoneyEscrowed
    self.orders[orderID].fxpSharesEscrowed = fxpSharesEscrowed
    self.orders[orderID].gasPrice = gasPrice
    log(type=MakeOrder, market, sender, type, fxpPrice, fxpAmount, outcome, orderID, fxpMoneyEscrowed, fxpSharesEscrowed, tradeGroupID)
    return(orderID)

# @internal
def removeOrder(orderID: address, type: uint8, market: address, outcome: uint8):
    self.controller.assertIsWhitelisted(msg.sender)
    require(tx.gasprice <= self.orders[orderID].gasPrice)
    self.removeOrderFromList(orderID, type, market, outcome)
    self.orders[orderID].fxpPrice = 0
    self.orders[orderID].fxpAmount = 0
    self.orders[orderID].owner = 0
    self.orders[orderID].fxpMoneyEscrowed = 0
    self.orders[orderID].fxpSharesEscrowed = 0
    self.orders[orderID].gasPrice = 0
    return(1)

# @internal
def fillOrder(orderID: address, orderType: int256, market: address, orderOutcome: int256, sharesFilled: int256, tokensFilled: int256):
    self.controller.assertIsWhitelisted(msg.sender)
    require(market)
    require(0 <= orderOutcome and orderOutcome < market.getNumberOfOutcomes())
    require(orderType == BID or orderType == ASK)
    require(orderID)
    require(sharesFilled <= self.orders[orderID].fxpSharesEscrowed)
    require(tokensFilled <= self.orders[orderID].fxpMoneyEscrowed)
    require(tx.gasprice <= self.orders[orderID].gasPrice)
    require(self.orders[orderID].fxpPrice <= market.getMaxDisplayPrice())
    require(self.orders[orderID].fxpPrice >= market.getMinDisplayPrice())
    require(market.getMaxDisplayPrice() + market.getMinDisplayPrice() <= 2**254)
    fill = 0
    if orderType == BID:
        # We can't use safeSub here because it disallows subtracting negative numbers. Worst case here is an operation of 2**254 - 1 as required above, which won't overflow
        fill = sharesFilled + safeFxpDiv(tokensFilled, self.orders[orderID].fxpPrice - market.getMinDisplayPrice())
    if orderType == ASK:
        # We can't use safeSub here because it disallows subtracting negative numbers. Worst case here is an operation of 2**254 - 1 as required above, which won't overflow
        fill = sharesFilled + safeFxpDiv(tokensFilled, market.getMaxDisplayPrice() - self.orders[orderID].fxpPrice)
    require(fill <= self.orders[orderID].fxpAmount)
    self.orders[orderID].fxpAmount -= fill
    self.orders[orderID].fxpMoneyEscrowed -= tokensFilled
    self.orders[orderID].fxpSharesEscrowed -= sharesFilled
    if self.orders[orderID].fxpAmount == 0:
        require(self.orders[orderID].fxpMoneyEscrowed == 0)
        require(self.orders[orderID].fxpSharesEscrowed == 0)
        self.removeOrderFromList(orderID, orderType, market, orderOutcome)
        self.orders[orderID].fxpPrice = 0
        self.orders[orderID].owner = 0
        self.orders[orderID].gasPrice = 0
        self.orders[orderID].betterOrderID = 0
        self.orders[orderID].worseOrderID = 0
    return(1)

# @internal
def takeOrderLog(market: address, orderOutcome: int256, orderType: int256, orderID: address, taker: address, makerSharesFilled: int256, makerTokensFilled: int256, takerSharesFilled: int256, takerTokensFilled: int256, tradeGroupID: uint256):
    self.controller.assertIsWhitelisted(msg.sender)
    price = self.orders[orderID].fxpPrice
    maker = self.orders[orderID].owner
    log(type=TakeOrder, market, orderOutcome, orderType, orderID, price, maker, taker, makerSharesFilled, makerTokensFilled, takerSharesFilled, takerTokensFilled, tradeGroupID)
    return(1)

# @internal
def completeSetsLog(sender, market, type, fxpAmount, numOutcomes, marketCreatorFee, reportingFee):
    self.controller.assertIsWhitelisted(msg.sender)
    log(type=CompleteSets, sender, market, type, fxpAmount, numOutcomes, marketCreatorFee, reportingFee)
    return(1)

# @internal
def cancelOrderLog(market, sender, fxpPrice, fxpAmount, orderID, outcome, type, fxpMoneyEscrowed, fxpSharesEscrowed):
    self.controller.assertIsWhitelisted(msg.sender)
    log(type=CancelOrder, market, sender, fxpPrice, fxpAmount, orderID, outcome, type, fxpMoneyEscrowed, fxpSharesEscrowed)
    return(1: uint256)

# @internal
def modifyMarketVolume(market: address, fxpAmount: uint256):
    self.controller.assertIsWhitelisted(msg.sender)
    self.marketOrderData[market].volume += fxpAmount
    market.getBranch().getTopics().updatePopularity(market.getTopic(), fxpAmount)
    return(1)

# @internal
def setPrice(market: address, outcome: uint8, fxpPrice: int256):
    self.controller.assertIsWhitelisted(msg.sender)
    self.marketOrderData[market].prices[outcome] = fxpPrice
    return(1: uint256)

# @private
def removeOrderFromList(orderID: address, type: uint8, market: address, outcome: uint8):
    assertPrivateCall()
    betterOrderID = self.orders[orderID].betterOrderID
    worseOrderID = self.orders[orderID].worseOrderID
    if(self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, type)] == orderID):
        self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, type)] = worseOrderID
    if(self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, type)] == orderID):
        self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, type)] = betterOrderID
    if(betterOrderID != 0):
        self.orders[betterOrderID].worseOrderID = worseOrderID
    if(worseOrderID != 0):
        self.orders[worseOrderID].betterOrderID = betterOrderID
    self.orders[orderID].betterOrderID = 0
    self.orders[orderID].worseOrderID = 0
    return(1)

# If best bid is not set or price higher than best bid price, this order is the new best bid.
# @private
def updateBestBidOrder(orderID: address, market: address, fxpPrice: int256, outcome: uint8, betterOrderID: int256):
    assertPrivateCall()
    bestBidOrderID = self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, BID)]
    if(bestBidOrderID == 0 or fxpPrice > self.orders[bestBidOrderID].fxpPrice):
        self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, BID)] = orderID
    return(self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, BID)]: address)

# If worst bid is not set or price lower than worst bid price, this order is the new worst bid.
# @private
def updateWorstBidOrder(orderID: address, market: address, fxpPrice: int256, outcome: uint8, worseOrderID: int256):
    assertPrivateCall()
    worstBidOrderID = self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, BID)]
    if(worstBidOrderID == 0 or fxpPrice < self.orders[worstBidOrderID].fxpPrice):
        self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, BID)] = orderID
    return(self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, BID)]: address)

# If best ask is not set or price lower than best ask price, this order is the new best ask.
# @private
def updateBestAskOrder(orderID: address, market: address, fxpPrice: int256, outcome: uint8, betterOrderID: int256):
    assertPrivateCall()
    bestAskOrderID = self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, ASK)]
    if(bestAskOrderID == 0 or fxpPrice < self.orders[bestAskOrderID].fxpPrice):
        self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, ASK)] = orderID
    return(self.bestOrder[self.getBestOrderWorstOrderHash(market, outcome, ASK)]: address)

# If worst ask is not set or price higher than worst ask price, this order is the new worst ask.
# @private
def updateWorstAskOrder(orderID: address, market: address, fxpPrice: int256, outcome: uint8, worseOrderID: int256):
    assertPrivateCall()
    worstAskOrderID = self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, ASK)]
    if(worstAskOrderID == 0 or fxpPrice > self.orders[worstAskOrderID].fxpPrice):
        self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, ASK)] = orderID
    return(self.worstOrder[self.getBestOrderWorstOrderHash(market, outcome, ASK)]: address)

def getBestOrderWorstOrderHash(market: address, outcome: uint8, type: uint8):
    orderInfo = [market, outcome, type]
    orderID = ripemd160(orderInfo, items=3)
    return(orderID)

# @controller
def setController(newController: address):
    require(msg.sender == self.controller)
    self.controller = newController
    return(1)

# @controller
def suicideFunds(to: address):
    require(msg.sender == self.controller)
    suicide(to)
